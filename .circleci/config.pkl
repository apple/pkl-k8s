amends "package://pkg.pkl-lang.org/pkl-project-commons/pkl.impl.circleci@1.0.0#/PklCI.pkl"

local buildWorkflow: Workflow = new {
  jobs {
    "build"
  }
}

triggerPackageDocsBuild = "release"

prb = buildWorkflow

main = buildWorkflow

release = (buildWorkflow) {
  jobs {
    new {
      ["do-release"] {
        requires {
          "build"
        }
        context {
          "pkl-github-release"
        }
      }
    }
  }
}

jobs {
  ["build"] {
    docker {
      new { image = "cimg/openjdk:17.0" }
    }
    steps {
      "checkout"
      new RunStep {
        command = "./gradlew build"
      }
      new PersistToWorkspaceStep {
        root = "."
        paths {
          "build/distributions/k8s/"
        }
      }
    }
  }
  ["do-release"] {
    docker {
      new { image = "maniator/gh:v2.40.1" }
    }
    steps {
      new AttachWorkspaceStep { at = "." }
      new RunStep {
        name = "gh release"
        command = #"""
          echo "Creating release"
          gh release create k8s@${CIRCLE_TAG} \
            --title "${CIRCLE_TAG}" \
            --target "${CIRCLE_SHA1}" \
            --notes "Release k8s version ${CIRCLE_TAG}" \
            --repo "${CIRCLE_PROJECT_USERNAME}/${CIRCLE_PROJECT_REPONAME}" \
            build/distributions/k8s/*
          """#
      }
    }
  }
}
